05 WIDTH 255
10 MAXITER = 24
20 LET C$ = "######################################"
30 FOR Y = -39 TO 39
40 FOR X = -39 TO 39
50 CREAL = X/20
70 CIMAG = Y/20
80 ZREAL  =  CREAL
90 ZIMAG  =  CIMAG
95 COUNT  =  1
100 ZM  =  ZREAL*ZREAL
105 ZN  =  ZIMAG*ZIMAG
107 ZL  =  ZM+ZN
110 IF ZL > 4 THEN GOTO 165
120 ZR2 = ZM-ZN+CREAL
130 ZIMAG = ZREAL*ZIMAG*2+CIMAG
140 ZREAL = ZR2
150 COUNT = COUNT+1
160 IF COUNT < MAXITER THEN GOTO 100
165 INK = 184-(7*COUNT)
166 GOSUB 1000
167 PAPER = 232+COUNT
168 GOSUB 1050
170 PRINT MID$(C$, 1+COUNT, 1);
180 NEXT X
185 PRINT ""
190 NEXT Y
200 END
1000 REM Ink Colour
1010 LET INK$ = STR$(INK)
1020 PRINT CHR$(27);"[38;5;";RIGHT$(INK$,LEN(INK$)-1);"m";
1030 RETURN
1050 REM Paper Colour
1060 PAPER$ = STR$(PAPER)
1070 PRINT CHR$(27);"[48;5;";RIGHT$(PAPER$,LEN(PAPER$)-1);"m";
1080 RETURN
